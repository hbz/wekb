package org.gokb.cred

import com.k_int.ClassUtils
import org.grails.web.json.JSONObject
import javax.persistence.Transient
import org.gokb.GOKbTextUtils
import groovy.util.logging.*
import java.time.LocalDateTime
import static grails.async.Promises.*


@Slf4j
class BookInstance extends TitleInstance {

  @Transient
  def titleLookupService

  String editionNumber
  String editionDifferentiator
  String editionStatement
  String volumeNumber
  String firstAuthor
  String firstEditor
  Date dateFirstInPrint
  Date dateFirstOnline
  String summaryOfContent

  private static refdataDefaults = [
    "TitleInstance.medium": "Book"
  ]

  static mapping = {
    includes TitleInstance.mapping
    editionNumber column: 'bk_ednum'
    editionDifferentiator column: 'bk_editionDifferentiator'
    editionStatement column: 'bk_editionStatement'
    volumeNumber column: 'bk_volume'
    dateFirstInPrint column: 'bk_dateFirstInPrint'
    dateFirstOnline column: 'bk_dateFirstOnline'
    summaryOfContent column: 'bk_summaryOfContent'
    firstAuthor column: 'bk_firstAuthor', type: 'text'
    firstEditor column: 'bk_firstEditor', type: 'text'
  }

  static constraints = {
    editionNumber (nullable:true, blank:false)
    /*editionNumber(validator: { val, obj ->
      if (obj.hasChanged('editionNumber') && val) {
        if (!(val ==~ "^\\d+\$")) {
          return ["typeMismatch.java.lang.Integer"]
        }
      }
    })*/
    editionDifferentiator(nullable: true, blank: false)
    editionStatement(nullable: true, blank: false)
    volumeNumber (nullable:true, blank:false)
    /*volumeNumber(validator: { val, obj ->
      if (obj.hasChanged('volumeNumber') && val) {
        if (!(val ==~ "^\\d+\$")) {
          return ["typeMismatch.java.lang.Integer"]
        }
      }
    })*/
    dateFirstInPrint(nullable: true, blank: false)
    dateFirstOnline(nullable: true, blank: false)
    summaryOfContent(nullable: true, blank: false)
    firstAuthor(nullable: true, blank: false)
    firstEditor(nullable: true, blank: false)
  }

  @Override
  String getLogEntityId() {
    "${this.class.name}:${id}"
  }

  @Override
  String getNiceName() {
    return "Book";
  }

  public static final String restPath = "/titles"


  /**
   * Auditable plugin, on change
   *
   * See if properties that might impact the mapping of this instance to a work have changed.
   * If so, fire the appropriate event to cause a remap.
   */
  def afterUpdate() {
    // Currently, serial items are mapped based on the name of the journal. We may need to add a discriminator property
    if ((hasChanged('name')) ||
      (hasChanged('editionStatement')) ||
      (hasChanged('componentDiscriminator'))) {
      log.debug("Detected an update to properties for ${id} that might change the work mapping. Looking up");
    }
    touchAllDependants()
  }

  @Override
  protected def generateComponentHash() {
    this.componentDiscriminator = generateBookDiscriminator(['volumeNumber': volumeNumber, 'editionDifferentiator': editionDifferentiator, 'firstAuthor': firstAuthor])
    // To try and find instances
    this.componentHash = GOKbTextUtils.generateComponentHash([normname, componentDiscriminator]);
    // To find works
    this.bucketHash = GOKbTextUtils.generateComponentHash([normname]);
  }


  def afterInsert() {
  }


  static String generateBookDiscriminator(Map relevantFields) {
    def result = null;
    def normVolume = generateNormname(relevantFields.volumeNumber)
    def normEdD = generateNormname(relevantFields.editionDifferentiator)
    def normFirstAuthor = generateNormname(relevantFields.firstAuthor)

    if (normVolume?.size() > 0 || normEdD?.size() > 0 || normFirstAuthor?.size() > 0) {
      result = "${normVolume ? 'v.' + normVolume : ''}${normEdD ? 'ed.' + normEdD : ''}${normFirstAuthor ? 'a:' + normFirstAuthor : ''}".toString()
    }
    result
  }


  @Deprecated
  static def validateDTO(JSONObject titleDTO, locale) {
    def result = TitleInstance.validateDTO(titleDTO, locale)
    def valErrors = [:]
    // shortening some db fields with standard size of 255 if needed.
    // does not invalidate the DTO!
    ['firstAuthor', 'firstEditor'].each { key ->
      if (titleDTO.containsKey(key)) {
        if (titleDTO[key].size() > 255) {
          valErrors.put(key,[message: "too long", baddata: titleDTO[key]])
          titleDTO[key] = titleDTO[key].substring(0, 251).concat(" ...")
          log.warn("value in key ’${key}’ was clipped to: ${titleDTO[key]}")
        }
      }
    }
    if (titleDTO.dateFirstInPrint) {
      LocalDateTime dfip = GOKbTextUtils.completeDateString(titleDTO.dateFirstInPrint, false)
      if (!dfip) {
        valErrors.put('dateFirstInPrint', [message: "Unable to parse", baddata: titleDTO.remove('dateFirstInPrint')])
      }
    }
    if (titleDTO.dateFirstOnline) {
      LocalDateTime dfo = GOKbTextUtils.completeDateString(titleDTO.dateFirstOnline, false)
      if (!dfo) {
        valErrors.put('dateFirstOnline', [message: "Unable to parse", baddata: titleDTO.remove('dateFirstOnline')])
      }
    }
    if (valErrors.size() > 0) {
      if (result.errors) {
        result.errors.putAll(valErrors)
      }
      else {
        result.errors = valErrors
      }
    }
    result
  }


  boolean addMonographFields(JSONObject titleObj) {
    def book_changed = false
    ["editionNumber", "editionDifferentiator",
     "editionStatement", "volumeNumber",
     "summaryOfContent", "firstAuthor",
     "firstEditor"].each { stringPropertyName ->
      if (titleObj[stringPropertyName] && titleObj[stringPropertyName].toString().trim().length() > 0) {
        book_changed |= ClassUtils.setStringIfDifferent(this, stringPropertyName, titleObj[stringPropertyName])
      }
    }
    def dfip = GOKbTextUtils.completeDateString(titleObj.dateFirstInPrint)
    book_changed |= ClassUtils.setDateIfPresent(dfip, this, 'dateFirstInPrint')
    def dfo = GOKbTextUtils.completeDateString(titleObj.dateFirstOnline, false)
    book_changed |= ClassUtils.setDateIfPresent(dfo, this, 'dateFirstOnline')
    book_changed
  }
}
